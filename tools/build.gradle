apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'io.horizontalsystems.tools'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation project(':bitcoincore')
    implementation project(':bitcoinkit')
    implementation project(':dashkit')
    implementation project(':bitcoincashkit')
    implementation project(':litecoinkit')
    implementation project(':ecashkit')
}

// Task to run Tools.main() from command line: ./gradlew tools:runTools
tasks.register('runTools', JavaExec) {
    dependsOn 'assembleDebug'

    mainClass = 'io.horizontalsystems.tools.ToolsKt'
    workingDir = rootProject.projectDir

    doFirst {
        def modules = ['bitcoincore', 'bitcoinkit', 'dashkit', 'bitcoincashkit', 'litecoinkit', 'ecashkit', 'hodler']
        def classDirs = modules.collect {
            file("${rootProject.projectDir}/${it}/build/intermediates/runtime_library_classes_dir/debug/bundleLibRuntimeToDirDebug")
        }
        // Add tools module classes from different location
        classDirs += file("${rootProject.projectDir}/tools/build/tmp/kotlin-classes/debug")

        def resourceDirs = [
            "${rootProject.projectDir}/bitcoincashkit/src/main/resources",
            "${rootProject.projectDir}/bitcoinkit/src/main/resources",
            "${rootProject.projectDir}/dashkit/src/main/resources",
            "${rootProject.projectDir}/ecashkit/src/main/resources",
            "${rootProject.projectDir}/litecoinkit/src/main/resources"
        ]

        // Collect all JAR dependencies from gradle cache
        def gradleUserHome = gradle.gradleUserHomeDir
        def jarFiles = fileTree("${gradleUserHome}/caches/modules-2/files-2.1").matching {
            include '**/*.jar'
        }.files

        classpath = files(classDirs) + files(resourceDirs) + files(android.bootClasspath) + files(jarFiles)
    }
}